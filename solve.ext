*-------------------------------------------------------------------------------
* External solver interface DEMO
*-------------------------------------------------------------------------------

$IF NOT %EXTSOL%==YES $GOTO POSTSOL

SET RS_PRETS(R,S,S);
RS_PRETS(R,S,S-RS_STG(R,S))$G_YRFR(R,S) = YES;
SET RTP_IPRI(R,ALLYEAR,P,CUR);  
OPTION RTP_IPRI<=OBJ_IPRIC; RTP_IPRI(R,LL,P,CUR)$(NOT T(LL))=NO;
STG_CHRG(R,'0',P,S) $= RPS_STG(R,P,S);

OPTION CLEAR=RTPCS_VARF,CLEAR=OBJ_1A,CLEAR=OBJ_1B,CLEAR=OBJ_2A,CLEAR=OBJ_2B;
OPTION CLEAR=OB_ICOST,CLEAR=OB_ITAX,CLEAR=OB_ISUB,CLEAR=OB_FOM,CLEAR=OB_FTX,CLEAR=OB_FSB;
SET RPFF_GGS(R,P,CG,CG,S);
OPTION FSCKS <= COEF_PTRAN;
LOOP(RPCC_FFUNC(RP(R,P),CG,CG2),OPTION CLEAR=MY_TS; Z=SUM(RPS_S1(RP,S),1);
  MY_TS(S)$RPS_S1(RP,S) = SUM((C,RS_TREE(R,S,TS))$FSCKS(R,P,CG,C,CG2,TS),1);
  IF(CARD(MY_TS)=Z,RPFF_GGS(RP,CG,CG2,ANNUAL)=YES; ELSE RPFF_GGS(RP,CG,CG2,MY_TS)=YES));

* Clean up some parameters
* Remove all non-V / non-RPCS_VAR from FLO_SHAR 
FLO_SHAR(R,LL,P,C,CG,S,BD)$(NOT RPCS_VAR(R,P,C,S)$V(LL))=0;
* RTPS_OFF / RTPCS_OUT: Make sure flows are bounded
* Remove all non-V / from PRC_ACTFLO -> define on MODLYR
PRC_ACTFLO(R,LL,P,CG)$(NOT V(LL))=0;

OPTION CLEAR=FSCKS;
$STOP
*-------------------------------------------------------------------------------
$LABEL POSTSOL
DISPLAY 'ENTERING POSTSOLVE';
SET VALT / LEVEL, DUAL /;

$OFFEPS
$ONDELIM
; TABLE OBJVAL(REG,OBV,CUR,VALT)
$INCLUDE OBJ.CSV
; TABLE NCAPVAL(REG,YEAR,PRC,VALT)
$INCLUDE NCAP.CSV
; TABLE CAPVAL(REG,YEAR,PRC,VALT)
$INCLUDE CAP.CSV
; TABLE ACTVAL(REG,YEAR,T,PRC,TS,VALT)
$INCLUDE ACT.CSV
; TABLE FLOVAL(REG,YEAR,T,PRC,COM,TS,VALT)
$INCLUDE FLOW.CSV
; TABLE IREVAL(REG,YEAR,T,PRC,COM,TS,IE,VALT)
$INCLUDE IRE.CSV
; TABLE BALVAL(REG,YEAR,COM,TS,VALT)
$INCLUDE COMBAL.CSV
EMPTY,EMPTY,EMPTY,EMPTY 0
; TABLE PRDVAL(REG,YEAR,COM,TS,VALT)
$INCLUDE COMPRD.CSV
EMPTY,EMPTY,EMPTY,EMPTY 0
;
$OFFDELIM
$CLEAR VAR_BLND EQ_IRE EQE_COMBAL EQ_PEAK
$CLEAR EQN_UCRTP EQE_UCRS EQE_UC EQE_UCR EQE_UCRT EQE_UCTS EQE_UCRTS EQE_UCSU EQE_UCSUS EQE_UCRSU EQE_UCRSUS

* Set results for variables
OBJZ.L = SUM((RDCUR(R,CUR),OBV),OBJVAL(R,OBV,CUR,'LEVEL'));
VAR_OBJ.L(R,OBV,CUR) $= OBJVAL(R,OBV,CUR,'LEVEL');
VAR_NCAP.L(R,V,P) $= NCAPVAL(R,V,P,'LEVEL');
VAR_NCAP.M(R,V,P) $= NCAPVAL(R,V,P,'DUAL');
VAR_CAP.L(R,V,P) $= CAPVAL(R,V,P,'LEVEL');
VAR_CAP.M(R,V,P) $= CAPVAL(R,V,P,'DUAL');
VAR_ACT.L(R,V,T,P,S) $= ACTVAL(R,V,T,P,S,'LEVEL');
VAR_ACT.M(R,V,T,P,S) $= ACTVAL(R,V,T,P,S,'DUAL');
VAR_FLO.L(R,V,T,P,C,S) $= FLOVAL(R,V,T,P,C,S,'LEVEL');
VAR_IRE.L(R,V,T,P,C,S,IE) $= IREVAL(R,V,T,P,C,S,IE,'LEVEL');
VAR_COMNET.L(R,T,C,S) $= BALVAL(R,T,C,S,'LEVEL');
VAR_COMPRD.L(R,T,C,S) $= PRDVAL(R,T,C,S,'LEVEL');
* Set results for equations
EQE_CPT.M(R,T,P)$CAPVAL(R,T,P,'DUAL') = -CAPVAL(R,T,P,'DUAL');
EQG_COMBAL.L(R,T,C,S) $= BALVAL(R,T,C,S,'LEVEL');
EQG_COMBAL.M(R,T,C,S) $= BALVAL(R,T,C,S,'DUAL');
EQE_COMPRD.M(R,T,C,S) $= PRDVAL(R,T,C,S,'DUAL');

$   BATINCLUDE rptmain.mod %1 NO_EMTY
$   IF NOT %TIMESED%==0
$   IF NOT %TIMESED%==YES $BATINCLUDE wrtbprice.mod
